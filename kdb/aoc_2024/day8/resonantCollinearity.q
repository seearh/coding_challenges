/
--- Day 8: Resonant Collinearity ---
You find yourselves on the roof of a top-secret Easter Bunny installation.

While The Historians do their thing, you take a look at the familiar huge antenna. Much to your surprise, it seems to have been reconfigured to emit a signal that makes people 0.1% more likely to buy Easter Bunny brand Imitation Mediocre Chocolate as a Christmas gift! Unthinkable!

Scanning across the city, you find that there are actually many such antennas. Each antenna is tuned to a specific frequency indicated by a single lowercase letter, uppercase letter, or digit. You create a map (your puzzle input) of these antennas. For example:

............
........0...
.....0......
.......0....
....0.......
......A.....
............
............
........A...
.........A..
............
............
The signal only applies its nefarious effect at specific antinodes based on the resonant frequencies of the antennas. In particular, an antinode occurs at any point that is perfectly in line with two antennas of the same frequency - but only when one of the antennas is twice as far away as the other. This means that for any pair of antennas with the same frequency, there are two antinodes, one on either side of them.

So, for these two antennas with frequency a, they create the two antinodes marked with #:

..........
...#......
..........
....a.....
..........
.....a....
..........
......#...
..........
..........
Adding a third antenna with the same frequency creates several more antinodes. It would ideally add four antinodes, but two are off the right side of the map, so instead it adds only two:

..........
...#......
#.........
....a.....
........a.
.....a....
..#.......
......#...
..........
..........
Antennas with different frequencies don't create antinodes; A and a count as different frequencies. However, antinodes can occur at locations that contain antennas. In this diagram, the lone antenna with frequency capital A creates no antinodes but has a lowercase-a-frequency antinode at its location:

..........
...#......
#.........
....a.....
........a.
.....a....
..#.......
......A...
..........
..........
The first example has antennas with two different frequencies, so the antinodes they create look like this, plus an antinode overlapping the topmost A-frequency antenna:

......#....#
...#....0...
....#0....#.
..#....0....
....0....#..
.#....A.....
...#........
#......#....
........A...
.........A..
..........#.
..........#.
Because the topmost A-frequency antenna overlaps with a 0-frequency antinode, there are 14 total unique locations that contain an antinode within the bounds of the map.

Calculate the impact of the signal. How many unique locations within the bounds of the map contain an antinode?

--- Part Two ---
Watching over your shoulder as you work, one of The Historians asks if you took the effects of resonant harmonics into your calculations.

Whoops!

After updating your model, it turns out that an antinode occurs at any grid position exactly in line with at least two antennas of the same frequency, regardless of distance. This means that some of the new antinodes will occur at the position of each antenna (unless that antenna is the only one of its frequency).

So, these three T-frequency antennas now create many antinodes:

T....#....
...T......
.T....#...
.........#
..#.......
..........
...#......
..........
....#.....
..........
In fact, the three T-frequency antennas are all exactly in line with two antennas, so they are all also antinodes! This brings the total number of antinodes in the above example to 9.

The original example now has 34 antinodes, including the antinodes that appear on every antenna:

##....#....#
.#.#....0...
..#.#0....#.
..##...0....
....0....#..
.#...#A....#
...#..#.....
#....#.#....
..#.....A...
....#....A..
.#........#.
...#......##
Calculate the impact of the signal using this updated model. How many unique locations within the bounds of the map contain an antinode?
\

\d .rc

/ Function that returns distinct coordinates of antinodes for a particular
/ Given 2D map input, antenna char & function that derives antinodes from antenna pairs
/ Assumes 2D map is square matrix
/ e.g. findAntinodesByAnt[<2D map input>;"A"]
findAntinodesByAnt:{[map;ant;f]
    pairs:2 cut/: c cross c:getCoords[map;ant];
    antinodes:raze f ./: pairs where not pairs[;0]~'pairs[;1];
    distinct antinodes where all each antinodes within (0;-1+count map)
 };

/ Function that returns coordinates of 2 antinodes, given a pair of antennas
/ e.g. findAntinodes[8 8;5 6]
findAntinodes:{(x+x-y;y+y-x)};

/ Function that returns 2D coordinates of specified antenna
/ e.g. getCoords[<2D map input>;"A";.rc.findAntinodes]
getCoords:{raze til[count x],/:'where each x=y};

p1:{
    ants:distinct[raze x] except ".";
    count distinct raze findAntinodesByAnt[x;;findAntinodes] each ants
 };

/ Function that returns all harmonic antinodes that lie within map
/ e.g. findHarmonicAntinodes[(0 11;0 11);0 0;1 3]
findHarmonicAntinodes:{[bounds;x;y] distinct[linspace[bounds;x;y],linspace[bounds;y;x]]};

/ Given
/   2 multi-dimensional coordinates X & Y
/   upper & lower bounds for each dimension
/ Function returns all coordinates along the vector X -> Y from X
/ until some bound is hit
/ e.g. linspace[(0 49;0 49);10 11;12 10]
linspace:{[bounds;x;y] flip x+d*\:til 1+min div[abs x-?[d>0;max each bounds;min each bounds];abs d:y-x]};

p2:{
    ants:distinct[raze x] except ".";
    count distinct raze findAntinodesByAnt[x;;findHarmonicAntinodes[flip (0;-1+(count x;count first x))]] each ants
 };

main:{
    input:read0`:./input.txt;
    show p1 input;
    show p2 input;
 };

\d .

if[.z.f~`resonantCollinearity.q;.rc.main`];